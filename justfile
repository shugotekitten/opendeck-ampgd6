id := "st.lynx.plugins.opendeck-ampgd6.sdPlugin"

release: bump package tag

package: build-linux build-mac build-win collect zip

bump next=`git cliff --bumped-version | tr -d "v"`:
    git diff --cached --exit-code

    echo "We will bump version to {{next}}, press any key"
    read ans

    sed -i 's/"Version": ".*"/"Version": "{{next}}"/g' manifest.json
    sed -i 's/^version = ".*"$/version = "{{next}}"/g' Cargo.toml

tag next=`git cliff --bumped-version`:
    echo "Generating changelog"
    git cliff -o CHANGELOG.md --tag {{next}}

    echo "We will now commit the changes, please review before pressing any key"
    read ans

    git add .
    git commit -m "chore(release): {{next}}"
    git tag "{{next}}"

build-linux:
    docker run --rm -it -v $(pwd):/io -w /io ghcr.io/rust-cross/cargo-zigbuild:sha-eba2d7e cargo zigbuild --release --target x86_64-unknown-linux-gnu --target-dir target/plugin-linux

build-mac:
    docker run --rm -it -v $(pwd):/io -w /io ghcr.io/rust-cross/cargo-zigbuild:sha-eba2d7e cargo zigbuild --release --target universal2-apple-darwin --target-dir target/plugin-mac

build-win:
    docker run --rm -it -v $(pwd):/io -w /io ghcr.io/rust-cross/cargo-zigbuild:sha-eba2d7e sh -c "apt-get update -qq && apt-get install -y -qq mingw-w64 > /dev/null 2>&1 && cargo zigbuild --release --target x86_64-pc-windows-gnu --target-dir target/plugin-win"

clean:
    sudo rm -rf target/

collect:
    rm -rf build
    mkdir -p build/{{id}}
    cp -r assets build/{{id}}
    cp manifest.json build/{{id}}
    cp target/plugin-linux/x86_64-unknown-linux-gnu/release/opendeck-ampgd6 build/{{id}}/opendeck-ampgd6-linux
    cp target/plugin-mac/universal2-apple-darwin/release/opendeck-ampgd6 build/{{id}}/opendeck-ampgd6-mac
    cp target/plugin-win/x86_64-pc-windows-gnu/release/opendeck-ampgd6.exe build/{{id}}/opendeck-ampgd6-win.exe

[working-directory: "build"]
zip:
    zip -r opendeck-ampgd6.plugin.zip {{id}}/
